{
  description = "nimbus-eth1";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs?rev=2a777ace4b722f2714cc06d596f2476ee628c04a";
    # WARNING: Do not use relative path, it breaks caching of NBS.
    # Remember to call 'nix flake update' when NBS is updated.
    nimbusBuildSystem = {
      url = "git+https://github.com/status-im/nimbus-build-system?submodules=1#";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    self = {
      # WARNING: Does not work with 'github:' schema URLs.
      # https://github.com/NixOS/nix/issues/14982
      submodules = true;
      # Avoid fetching big files from vendor/hoodi submodule.
      lfs = false;
    };
  };

  outputs = { self, nixpkgs, nimbusBuildSystem }:
    assert (builtins.compareVersions builtins.nixVersion "2.27") <= 0
      -> throw "Nix 2.27 or newer needed for proper submodules support!";

    let
      stableSystems = [
        "x86_64-linux" "aarch64-linux" "armv7a-linux"
        "x86_64-darwin" "aarch64-darwin"
        "x86_64-windows"
      ];
      forEach = nixpkgs.lib.genAttrs;
      forAllSystems = forEach stableSystems;
      pkgsFor = forEach stableSystems (
        system: import nixpkgs { inherit system; }
      );
    in rec {
      packages = forAllSystems (system: let
        buildTarget = pkgsFor.${system}.callPackage ./nix/default.nix {
          inherit stableSystems self;
        };
        nim = nimbusBuildSystem.packages.${system}.nim;
        build = targets: buildTarget.override { inherit targets nim; };
      in rec {
        nimbus                  = build ["nimbus"];
        nimbus_execution_client = build ["nimbus_execution_client"];
        nimbus_portal_bridge    = build ["portal_bridge"];
        nimbus_portal_client    = build ["nimbus_portal_client"];
        nimbus_verified_proxy   = build ["nimbus_verified_proxy"];
        nimbus_fluffy           = build ["fluffy"];

        default = nimbus;
      });

      devShells = forAllSystems (system: {
        default = pkgsFor.${system}.callPackage ./nix/shell.nix {
          inherit (nimbusBuildSystem.packages.${system}) nim;
        };
      });
    };
}
